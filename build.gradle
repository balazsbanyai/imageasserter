apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

repositories {
    jcenter()
}

dependencies {
    implementation group: 'nu.pattern', name: 'opencv', version: '2.4.9-4'
    implementation 'org.hamcrest:hamcrest-library:1.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

test {
    useJUnitPlatform()
}


task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.banyaibalazs.imageasserter'
            artifactId 'imageasserter'
            version '2.0.0'

            from components.java
            artifact sourceJar
            artifact packageJavadoc
        }
    }

    repositories {
        maven {
            name 'bintray'
            url "https://api.bintray.com/maven/balazsbanyai/imageasserter/imageasserter/;publish=1"
            credentials {
                username System.getenv('BINTRAY_USER')
                password System.getenv('BINTRAY_KEY')
            }
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor false

        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}


jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport