
subprojects {

    repositories {
        jcenter()
    }


    apply plugin: 'java'

    sourceSets {
        test {
            resources {
                srcDirs += ["${rootProject.projectDir}/test-res/src/main/resources"]
            }
        }
    }

    javadoc {
        source = sourceSets.main.allJava
        classpath = configurations.compileClasspath
        options {
            setMemberLevel JavadocMemberLevel.PUBLIC
            setAuthor false

            links "https://docs.oracle.com/javase/8/docs/api/"
        }
    }

    test {
        useJUnitPlatform()
    }


    apply plugin: 'jacoco'

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    check.dependsOn jacocoTestReport


    apply plugin: 'maven-publish'

    tasks.withType(AbstractPublishToMaven) { dependsOn check }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId 'com.banyaibalazs.imageasserter'
                artifactId project.name
                version '2.0.0'

                from components.java
                artifact project.tasks.create(name: 'sourcesJar', type: Jar) {
                    classifier = 'sources'
                    from sourceSets.main.allJava
                }
                artifact project.tasks.create(name: 'javadocJar', type: Jar) {
                    from javadoc
                    classifier = 'javadoc'
                }
            }
        }

        repositories {
            maven {
                name 'bintray'
                url "https://api.bintray.com/maven/balazsbanyai/imageasserter/imageasserter/;publish=1"
                credentials {
                    username System.getenv('BINTRAY_USER')
                    password System.getenv('BINTRAY_KEY')
                }
            }
        }
    }

}